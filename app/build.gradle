apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: "dependencies.gradle"

android {
    compileSdkVersion compileSdk
    defaultConfig {
        applicationId appPackage
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode appCode
        versionName appName
        testInstrumentationRunner testRunner
        vectorDrawables.useSupportLibrary = true

        Properties properties = new Properties()
        if (project.rootProject.file("mapbox.properties").exists()) {
            properties.load(project.rootProject.file("mapbox.properties").newDataInputStream())
            properties.load(project.rootProject.file("foursquare.properties").newDataInputStream())

            buildConfigField "String", "MAPBOX_API_KEY", properties.getProperty("MAPBOX_API_KEY")
            buildConfigField "String", "CLIENT_ID", properties.getProperty("FOURSQUARE_CLIENT_ID")
            buildConfigField "String", "CLIENT_SECRET", properties.getProperty("FOURSQUARE_CLIENT_SECRET")
        } else {
            buildConfigField "String", "MAPBOX_API_KEY", System.getenv('MAPBOX_API_KEY') ?: '""'
            buildConfigField "String", "CLIENT_ID", System.getenv('FOURSQUARE_CLIENT_ID') ?: '""'
            buildConfigField "String", "CLIENT_SECRET", System.getenv('FOURSQUARE_CLIENT_SECRET') ?: '""'
        }
        buildConfigField "String", "FOURSQUARE_API", '"https://api.foursquare.com/v2/"'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation kotlin
    implementation support.appcompat
    implementation support.design
    implementation support.cardview
    implementation support.constraint

    kapt dagger.compiler
    implementation dagger.library

    implementation rx.java
    implementation rx.android

    implementation network.retrofit.library
    implementation network.retrofit.gson
    implementation network.retrofit.rxAdapter

    implementation network.okhttp.library
    implementation network.okhttp.logging

    implementation cache.room.runtime
    implementation cache.room.rx
    testImplementation cache.room.runtime
    kapt cache.room.compiler

    implementation network.glide.library
    kapt network.glide.compiler

    implementation mapbox
    implementation dexter

    debugImplementation roomDebugger

    testImplementation testing.jUnit
    testImplementation testing.mockito.core
    testImplementation testing.mockito.kotlin

    kaptAndroidTest dagger.compiler
    kaptAndroidTest cache.room.compiler

    androidTestImplementation testing.mockito.core
    androidTestImplementation testing.mockito.kotlin
    androidTestImplementation testing.mockito.android

    androidTestImplementation testing.espresso.core
    androidTestImplementation testing.espresso.runner
    androidTestImplementation testing.espresso.rules
    androidTestImplementation testing.espresso.intents

    androidTestImplementation testing.espresso.barista

    androidTestImplementation testing.dagger.mock
    androidTestImplementation testing.dagger.kotlin

    testImplementation cache.room.test

    androidTestImplementation testing.dexOpener
}
